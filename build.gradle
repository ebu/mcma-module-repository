import groovy.json.JsonOutput
import groovy.json.JsonSlurper

def McmaVersion = "0.8.7"

def requiredVersionTerraform = "0.12.19"
def requiredVersionNodeJS = "10.16.3"
def requiredVersionTypeScript = "3.7.2"

allprojects {
    ext {
        if (org.gradle.internal.os.OperatingSystem.current().isWindows()) {
            nodeExecutable = Arrays.asList("node")
            ngExecutable = Arrays.asList("cmd", "/c", "ng")
            npmExecutable = Arrays.asList("cmd", "/c", "npm")
            terraformExecutable = Arrays.asList("cmd", "/c", "terraform")
            tscExecutable = Arrays.asList("cmd", "/c", "tsc")
        } else {
            nodeExecutable = Arrays.asList("node")
            ngExecutable = Arrays.asList("ng")
            npmExecutable = Arrays.asList("npm")
            terraformExecutable = Arrays.asList("terraform")
            tscExecutable = Arrays.asList("tsc")
        }

        awsAvailableLibraries = Arrays.asList(".bin", "aws-sdk", "buffer", "dynamodb-doc", "events", "ieee754", "jmespath", "lodash", "punycode", "querystring", "url")
    }
}

def getVersionNodeJS = { ->
    def stdout = new ByteArrayOutputStream()
    def stderr = new ByteArrayOutputStream()
    try {
        exec {
            commandLine nodeExecutable
            args "--version"
            standardOutput stdout
            errorOutput stderr
        }
    } catch (Exception ignored) {
    }

    return stdout.toString().trim().replace("v", "")
}

task verifyNodeJS {
    inputs.property("VERSION_NODE_JS", requiredVersionNodeJS)
    outputs.upToDateWhen { getVersionNodeJS() == requiredVersionNodeJS }
    doLast {
        def versionNodeJs = getVersionNodeJS()
        if (versionNodeJs != requiredVersionNodeJS) {
            throw new Exception("Required Node version " + requiredVersionNodeJS + " not installed. Detected version " + versionNodeJs)
        }
    }
}

def getVersionTerraform = { ->
    def stdout = new ByteArrayOutputStream()
    def stderr = new ByteArrayOutputStream()
    try {
        exec {
            commandLine terraformExecutable
            args "--version"
            standardOutput stdout
            errorOutput stderr
        }
    } catch (Exception ignored) {
    }

    return stdout.toString().split("\n")[0].trim().replace("Terraform v", "")
}

task verifyTerraform {
    inputs.property("VERSION_TERRAFORM", requiredVersionTerraform)
    outputs.upToDateWhen { getVersionTerraform() == requiredVersionTerraform }
    doLast {
        if (getVersionTerraform() != requiredVersionTerraform) {
            throw new Exception("Required Terraform version " + requiredVersionTerraform + " not available")
        }
    }
}

def getVersionTypeScript = { ->
    def stdout = new ByteArrayOutputStream()
    def stderr = new ByteArrayOutputStream()
    try {
        exec {
            commandLine tscExecutable
            args "--v"
            standardOutput stdout
            errorOutput stderr
        }
    } catch (Exception ignored) {
    }

    return stdout.toString().trim().replace("Version ", "")
}

task verifyTypeScript {
    inputs.property("VERSION_TYPESCRIPT", requiredVersionTypeScript)
    outputs.upToDateWhen { getVersionTypeScript() == requiredVersionTypeScript }
    doLast {
        if (getVersionTypeScript() != requiredVersionTypeScript) {
            throw new Exception("Required TypeScript version " + requiredVersionTypeScript + " not available")
        }
    }
}

task build {
    dependsOn "service:build"
}

task clean {}

subprojects {
    task setMcmaVersion {
        def packageJson = file("package.json")

        enabled packageJson.exists()
        inputs.file(packageJson)
        inputs.property("VERSION", McmaVersion)
        outputs.file(packageJson)

        doLast {
            def jsonSlurper = new JsonSlurper()
            def object = jsonSlurper.parse(file("package.json"))

            if (object.dependencies != null) {
                object.dependencies.keySet().each {
                    object.dependencies[it] = it.startsWith("@mcma/") ? McmaVersion : object.dependencies[it]
                }
            }
            if (object.peerDependencies != null) {
                object.peerDependencies.keySet().each {
                    object.peerDependencies[it] = it.startsWith("@mcma/") ? McmaVersion : object.peerDependencies[it]
                }
            }
            if (object.devDependencies != null) {
                object.devDependencies.keySet().each {
                    object.devDependencies[it] = it.startsWith("@mcma/") ? McmaVersion : object.devDependencies[it]
                }
            }

            def json = JsonOutput.toJson(object)
            def jsonPretty = JsonOutput.prettyPrint(json) + "\n"

            packageJson.write(jsonPretty)
        }
    }

    task npmInstall(type: Exec) {
        dependsOn verifyNodeJS
        enabled file("package.json").exists()
        inputs.file("package.json")
        outputs.file("package-lock.json")
        outputs.dir("node_modules")
        commandLine npmExecutable
        args "install"
    }

    task npmUpdate(type: Exec) {
        dependsOn verifyNodeJS
        enabled file("package.json").exists()
        inputs.file "package.json"
        inputs.property("todaysDate", new Date().clearTime())
        outputs.upToDateWhen { true }
        commandLine npmExecutable
        args "update"
    }

    task npmDedup(type: Exec) {
        dependsOn verifyNodeJS
        enabled file("package.json").exists()
        inputs.file("package.json")
        outputs.file("package-lock.json")
        outputs.dir("node_modules")
        commandLine npmExecutable
        args "dedup"
    }

    task clean(type: Delete) {
        delete "build"
        delete "node_modules"
    }
}

afterEvaluate {
    // avoiding simultaneous connections to npmjs.com
    def prevTask = null
    project.subprojects.each {
        def task = it.tasks.find { task -> task.name.contains('npmUpdate') }
        if (task != null) {
            if (prevTask != null) {
                task.mustRunAfter(prevTask)
            }
            prevTask = task
        }
    }

    prevTask = null
    project.subprojects.each {
        def task = it.tasks.find { task -> task.name.contains('npmInstall') }
        if (task != null) {
            if (prevTask != null) {
                task.mustRunAfter(prevTask)
            }
            prevTask = task
        }
    }
}
